1000 '---------------------------------
1010 '
1020 'BEER 202 1.8 Partitioning program
1030 ' (c) 2005 A&L Software
1040 '
1050 '---------------------------------
1060 '
1070 ' Initialization
1080 '
1090 SCREEN 0:COLOR 15,1,1:WIDTH 40:KEY OFF:VPOKE 2411,255
1100 GOSUB 2300:A$="Initializing...":B$="Please wait":GOSUB 2870
1110 BLOAD"BEERFDSK.BIN"
1120 DEFFNS$(X)=MID$(STR$(X),2):DEFUSR=&HC005:DEFUSR1=&HC008:DEFUSR2=&HC00B:DEFUSR3=&HC00E:DEFUSR4=&HC011:DEFUSR5=&HC014:DEFUSR6=&HC017:DEFUSR7=&HC01A:DEFUSR8=&HC01D
1130 A=USR6(0):DIM SI(5),CY(5),BO(5),FA(5):GOSUB 1500:MO=0:Y=1
1140 '
1150 ' Main screen
1160 '
1170 LOCATE 0,10:PRINT SPC(160):LOCATE 0,5:GOSUB 1660
1180 LOCATE 0,14:PRINT"XWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWY";
1190 FOR I=1 TO 5:GOSUB 1770:NEXT
1200 PRINT"ZWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW[";
1210 PRINT"[C]reate [E]rase [B]oot [F]ormat  [Q]uit";
1220 PRINT"[I]nitialize all    [W]rite    [D]iscard";
1230 LOCATE 0,14+Y:A=USR7(0)
1240 GOSUB 2350
1250 IF B$=CHR$(31) THEN A=USR7(0):Y=Y+1:IF Y=6 THEN Y=1:GOTO 1230 ELSE GOTO 1230
1260 IF B$=CHR$(30) THEN A=USR7(0):Y=Y-1:IF Y=0 THEN Y=5:GOTO 1230 ELSE GOTO 1230
1270 IF B$="B" AND CY(Y)<>0 THEN BO(Y)=255-BO(Y):I=Y:GOSUB 1770:MO=1:GOTO 1230
1280 IF B$="E" AND CY(Y)<>0 THEN A$="The partition will be deleted.":GOSUB 2750:IF B$="Y" THEN CY(Y)=0:MO=1:GOTO 1170 ELSE GOTO 1170
1290 IF B$="D" THEN IF MO=0 THEN GOSUB 1610:GOTO 1170 ELSE A$="All changes will be lost.":GOSUB 2750:IF B$="Y" THEN MO=0:GOSUB 1610:GOTO 1170 ELSE GOTO 1170
1300 IF B$="Q" THEN IF MO=0 THEN SCREEN 0:KEY ON:END ELSE A$="All changes will be lost.":GOSUB 2750:IF B$="Y" THEN SCREEN 0:KEY ON:END ELSE GOTO 1170
1310 IF B$="C" AND CY(Y)=0 THEN GOSUB 1850:MO=MO-(CY(Y)<>0):GOTO 1170
1320 IF B$="W" AND MO<>0 THEN A$="Previous partition table will be lost!":GOSUB 2750:IF B$="Y" THEN GOSUB 1990:MO=0:GOTO 1170 ELSE GOTO 1170
1330 IF B$="F" AND CY(Y)<>0 THEN GOSUB 2160:IF B$="Y" THEN I=Y:GOSUB 2090:GOTO 1170 ELSE GOTO 1170
1340 IF B$="I" THEN A=0:FOR I=1 TO 5:A=A+(CY(I)<>0):NEXT:IF A<>0 THEN GOSUB 2160:IF B$="Y" THEN GOSUB 2210:GOTO 1170 ELSE GOTO 1170
1350 GOTO 1240
1360 '
1370 ' Parse partition table
1380 '
1390 FOR I=1 TO 5:CY(I)=0
1400 CY(I)=PEEK(&HA0F0+I*16)+PEEK(&HA0F1+I*16)*256:BO(I)=PEEK(&HA0F2+I*16):FA(I)=PEEK(&HA0F3+I*16)
1410 FOR J=4 TO 15:IF PEEK(&HA0F0+I*16+J)<>0 THEN CY(I)=0 ELSE NEXT
1420 IF FA(I)<>0 THEN CY(I)=0
1430 NEXT:FOR I=1 TO 5:IF CY(I)=0 THEN 1460
1440 HH1=INT(CY(I)/256):POKE&HC002,HH1
1441 HK2=CY(I)-(256*(INT(CY(I)/256))):POKE&HC001,HK2
1442 A=USR5(0)
1443 IF PEEK(&HC003)<>0 THEN GOSUB 2590:IF B$="I" THEN CY(I)=0 ELSE 1440
1450 SI(I)=PEEK(&HA013)+256*PEEK(&HA014)
1460 NEXT:RETURN
1470 '
1480 ' Get BEER and HD data
1490 '
1500 A=USR(0):IF PEEK(&HC000)=255 THEN 2390
1510 SL$=FNS$(PEEK(&HC000) AND 3)+"."+FNS$((PEEK(&HC000) AND 12)/4)
1520 BV$=HEX$(PEEK(&HC002))+"."+HEX$(PEEK(&HC001))
1530 IF PEEK(&HC002)<1 OR PEEK(&HC001)<&H80 THEN 2430
1540 A=USR1(0):IF PEEK(&HC003)<>0 THEN 2470
1550 I=54:J=20:GOSUB 2260:HM$=A$
1560 I=20:J=10:GOSUB 2260:HS$=A$
1570 I=46:J=4:GOSUB 2260:HR$=A$
1580 DC=PEEK(&HA002)+256*PEEK(&HA003):DH=PEEK(&HA006)+256*PEEK(&HA007):DS=PEEK(&HA00C)+256*PEEK(&HA00D):DT=INT((DC/2048)*DH*DS):DM=INT(DS*32)
1590 IF DH<>PEEK(&HFD0C) THEN 2510
1600 IF DS<>PEEK(&HFD0B) THEN 2510
1610 A=USR2(0):IF PEEK(&HC003)<>0 THEN 2550 ELSE GOSUB 1390:RETURN
1620 '
1630 ' Print BEER and HD data
1640 '
1650 GOSUB 2300
1660 PRINT"Interface slot:";SL$
1670 PRINT"BIOS version  :";BV$
1680 PRINT"Disk model    :";HM$
1690 PRINT"Disk S/N      :";HS$
1700 PRINT"Disk revision :";HR$
1710 PRINT"Disk geometry :"FNS$(DC)"/"FNS$(DH)"/"FNS$(DS)"(CHS),"FNS$(DT)"MB"
1720 PRINT"Addressable   :"FNS$(DM)"MB"
1730 RETURN
1740 '
1750 ' Print partition table entry data
1760 '
1770 PRINT"VPart "FNS$(I)":";
1780 IF CY(I)=0 THEN PRINT LEFT$("        --- UNUSED ---"+SPACE$(31),31)"V";:RETURN
1790 PRINT RIGHT$("     "+FNS$(INT(SI(I)/2)), 5)"KB Trk."LEFT$(FNS$(CY(I))+"     ",5)" ";
1800 IF BO(I) THEN PRINT"  Boot"; ELSE PRINT"      ";
1810 PRINT"  FAT12V";:RETURN
1820 '
1830 ' Create partition entry
1840 '
1850 MX=65535!:IF Y<5 THEN FOR I=Y+1 TO 5:IF CY(I)<>0 THEN MX=CY(I)-1 ELSE NEXT
1860 FOR I=1 TO Y-1:IF CY(I)<>0 THEN MX=MX-(INT(SI(I)/DS)+1):NEXT ELSE NEXT
1870 MX=INT(MX*DS/2)
1880 IF MX>32767 THEN MX=32767
1890 A$="Enter partition size (180KB-"+LEFT$(FNS$(MX)+"KB)     ",8):B$="Partition size: _____ KB"+SPACE$(12):GOSUB 2870
1900 A$="":I=0:LOCATE 18,12:PRINT"_____"
1910 LOCATE 18+I,12:B$=INPUT$(1):IF B$=CHR$(13) THEN 1940 ELSE IF B$=CHR$(8) AND I>0 THEN I=I-1:LOCATE 18+I,12:PRINT"_":A$=LEFT$(A$,I):GOTO 1910
1920 IF B$>="0" AND B$<="9" THEN IF I<5 THEN PRINT B$:I=I+1:A$=A$+B$:GOTO 1910 ELSE BEEP:GOTO 1910
1930 GOTO 1910
1940 IF A$="" THEN RETURN ELSE IF VAL(A$)>MX OR VAL(A$)<180 THEN BEEP:GOTO 1900
1950 BO(Y)=0:FA(Y)=0:SI(Y)=VAL(A$)*2:CY(Y)=1:IF Y=1 THEN RETURN ELSE FOR I=1 TO Y-1:IF CY(I)<>0 THEN CY(Y)=CY(Y)+INT((SI(I)/DS)+1):NEXT:RETURN ELSE NEXT:RETURN
1960 '
1970 ' Write partition table to medium
1980 '
1990 A$="Writing partition table":B$="Please wait":GOSUB 2870
2000 POKE &HC003,0:A=USR8(0):POKE &HA000,&HC9:POKE &HA200,&HC9
2010 FOR I=1 TO 5:A=&HA0F0+I*16:POKE A,CY(I) MOD 256:POKE A+&H200,PEEK(A):POKE A+1,CY(I)\256:POKE A+&H201,PEEK(A+1):POKE A+2,BO(I):POKE A+&H202,PEEK(A+2):POKE A+3,FA(I):POKE A+&H203,PEEK(A+3):NEXT
2020 A=USR3(0):IF PEEK(&HC003)<>0 THEN GOSUB 2630:IF B$="R" THEN 2020 ELSE RETURN
2030 POKE &HC003,255:A=USR8(0):A=USR2(0):IF PEEK(&HC003)<>0 THEN A$="Error reading partition table!":GOSUB 2790:IF B$="R" THEN 2030 ELSE RETURN
2040 POKE &HC003,254:A=USR8(0):IF PEEK(&HC003)=0 THEN RETURN
2050 GOSUB 2670:IF B$="R" THEN 1990 ELSE RETURN
2060 '
2070 ' Format a partition
2080 '
2090 A$="Formatting partition "+FNS$(Y):B$="Please wait":GOSUB 2870
2100 POKE &HC002,INT(SI(Y)/256)
2101 POKE &HC001,SI(Y)-(INT(SI(Y)/256)*256)
2102 POKE &HC004,INT(CY(Y)/256)
2103 POKE &HC003,CY(Y)-(256*(INT(CY(Y)/256)))
2110 A=USR4(0):IF PEEK(&HC003)<>0 THEN GOSUB 2710:IF B$="R" THEN 2100
2120 RETURN
2130 '
2140 ' Confirm partition formatting
2150 '
2160 IF MO<>0 THEN A$="Partition table not written yet.":B$="Press any key to continue":GOSUB 2870:GOSUB 2350:B$="N":RETURN
2170 A$="All data will be lost!":GOSUB 2750:RETURN
2180 '
2190 ' Format all partitions
2200 '
2210 FOR Y=1 TO 5:IF CY(Y)<>0 THEN GOSUB 2090
2220 NEXT:Y=1:RETURN
2230 '
2240 ' Get a fixed size string
2250 '
2260 A$="":FOR K=1 TO J:A$=A$+CHR$(PEEK(&H9FFF+I+K)):NEXT:RETURN
2270 '
2280 ' Print program copyright
2290 '
2300 CLS
2310 PRINT"X--------------------------------------YVBEER 202 Partitioning program         VV32 Mb partition creation fixed        VZ--------------------------------------[":RETURN
2320 '
2330 ' Wait for a key
2340 '
2350 B$=INKEY$:IF B$="" THEN 2350 ELSE B$=CHR$(ASC(B$) AND 223):RETURN
2360 '
2370 ' Fatal error: BEER not found
2380 '
2390 A$="BEER 202 Interface not found!":GOTO 2830
2400 '
2410 ' Fatal error: wrong BEER version
2420 '
2430 A$="BEER 202 BIOS version lower than 1.80!":GOTO 2830
2440 '
2450 ' Fatal error: can't identify HD
2460 '
2470 A$="Error getting drive identification!":GOTO 2830
2480 '
2490 ' Fatal error: BEER not init
2500 '
2510 A$="BEER not correctly initialized!":GOTO 2830
2520 '
2530 ' Fatal error: can't read part tbl
2540 '
2550 A$="Error reading partition table!":GOTO 2830
2560 '
2570 ' Error: can't read partition boot
2580 '
2590 A$="Error reading partition boot!":GOTO 2790
2600 '
2610 ' Error: can't write part boot
2620 '
2630 A$="Error writing partition table!":GOTO 2790
2640 '
2650 ' Error: part table not persistent
2660 '
2670 A$="Partition table not persistent!":GOTO 2790
2680 '
2690 ' Error: error formatting part
2700 '
2710 A$="Error formatting partition!":GOTO 2790
2720 '
2730 ' Ask user confirmation
2740 '
2750 B$="Are you sure (Y/N)?":GOSUB 2870:GOSUB 2350:IF B$<>"Y" AND B$<>"N" THEN 2750 ELSE RETURN
2760 '
2770 ' Ask for abort/retry/ignore
2780 '
2790 B$="[R]etry, [A]bort or [I]gnore?":GOSUB 2870:BEEP:BEEP:BEEP:GOSUB 2350:IF B$="A" THEN SCREEN 0:KEY ON:END ELSE IF B$<>"R" AND B$<>"I" THEN 2790 ELSE RETURN
2800 '
2810 ' Ask for a key and abort
2820 '
2830 B$="Press any key to quit.":GOSUB 2870:BEEP:BEEP:BEEP:GOSUB 2350:SCREEN 0:KEY ON:END
2840 '
2850 ' Print message boxed and centered
2860 '
2870 J=LEN(A$):I=INT((40-J-2)/2):IF LEN(B$)>J THEN J=LEN(B$):I=INT((40-J-2)/2)
2880 LOCATE I,10:PRINT"X"STRING$(J,"-")"Y":LOCATE I,11:PRINT"V"SPC(J)"V":LOCATE I,12:PRINT"V"SPC(J)"V":LOCATE I,13:PRINT"Z"STRING$(J,"-")"[":LOCATE INT((40-LEN(A$))/2),11:PRINT A$:LOCATE INT((40-LEN(B$))/2),12:PRINT B$:RETURN
